/*
 * Copyright 2021 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java-library'

apply from: "${project.scripts_url}/${project.scripts_branch}/build-quality.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/release-commons.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/signing.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/jacoco.gradle"

project.ext.limits = [
        'instruction': 70,
        'branch'     : 53,
        'line'       : 75,
        'complexity' : 60,
        'method'     : 65,
        'class'      : 87
]

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    api 'com.epam.reportportal:client-java:5.2.6'

    compileOnly "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.slf4j:slf4j-api:2.0.7'

    testImplementation 'com.epam.reportportal:agent-java-test-utils:0.0.3'
    testImplementation "org.junit.platform:junit-platform-runner:${junit_runner_version}" // We need runner for JUnit 4 compatibility tests
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junit_version}" // Required for tests to run
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit_version}"
    testImplementation 'org.aspectj:aspectjweaver:1.9.19'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'ch.qos.logback:logback-classic:1.3.12'
    testImplementation 'com.epam.reportportal:logger-java-logback:5.2.2'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'com.squareup.okhttp3:okhttp:4.12.0'
}

test {
    outputs.upToDateWhen { return false }
    useJUnitPlatform()
    exclude("com/epam/reportportal/junit5/features/**")
    maxParallelForks(5) // it's forks - separate JVMs, should not interfere each other
    forkEvery(1)
    doFirst {
        def weaver = configurations.testRuntimeClasspath.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
    environment "AGENT_NO_ANALYTICS", "1"
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}

release {
    git {
        requireBranch = 'master'
    }
}

build.dependsOn jacocoTestReport
